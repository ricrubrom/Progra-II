programa Ejercicio4
procesos
  proceso Carrera
  variables 
    actCa, actAv: numero
    hayPapel: boolean
  comenzar 
    hayPapel := V
    actAv := PosAv
    mientras((hayPapel) & (PosCa < 99)) 
      actCa := PosCa
      BloquearEsquina(11, 11)
      Pos(11, 11)
      si(HayPapelEnLaEsquina)
        tomarPapel
      sino
        hayPapel := F
        Informar(1)
      Pos(actAv, actCa)
      LiberarEsquina(11, 11)
      si(hayPapel)
        depositarPapel
        mover
    si(PosCa = 100)
      BloquearEsquina(11, 11)
      Pos(11, 11)
      mientras(HayPapelEnLaEsquina)
        tomarPapel 
      Pos(actAv, actCa)
      LiberarEsquina(11, 11)
  fin
areas
  AreaCD : AreaP(1,1,1,1)
  AreaR1 : AreaP(4, 1, 4, 100)
  AreaR2 : AreaP(6, 1, 6, 100)
  AreaR3 : AreaP(8, 1, 8, 100)
  AreaR4 : AreaP(10, 1, 10, 100)
  Area11 : AreaC(11, 11, 11, 11)
robots
  robot corredor
  variables
    quienSoy: numero
  comenzar
    RecibirMensaje(quienSoy, CD)
    Carrera
    EnviarMensaje(quienSoy, CD)
    EnviarMensaje(PosCa, CD)
  fin

  robot coordinador
  variables
    max, rob, maxRobot, recorrido : numero
  comenzar
    EnviarMensaje(1,R1)
    EnviarMensaje(2,R2)
    EnviarMensaje(3,R3)
    EnviarMensaje(4,R4)
    max := 0
    repetir(4)
      RecibirMensaje(rob, *)
      Informar(1)
      si(rob = 1)
        RecibirMensaje(recorrido, R1)
      sino 
        si(rob = 2)
          RecibirMensaje(recorrido, R2)
        sino 
          si(rob = 3)
            RecibirMensaje(recorrido, R3)
          sino
            RecibirMensaje(recorrido, R4)
      si(recorrido > max)
        max := recorrido
        maxRobot := rob
    Informar(maxRobot)
  fin

variables
  R1, R2, R3, R4 : corredor
  CD : coordinador
comenzar
  AsignarArea(R1, AreaR1)
  AsignarArea(R1, Area11)
  
  AsignarArea(R2, AreaR2)
  AsignarArea(R2, Area11)

  AsignarArea(R3, AreaR3)
  AsignarArea(R3, Area11)

  AsignarArea(R4, AreaR4)
  AsignarArea(R4, Area11)

  AsignarArea(CD, AreaCD)

  Iniciar(R1, 4, 1)
  Iniciar(R2, 6, 1)
  Iniciar(R3, 8, 1)
  Iniciar(R4, 10, 1)
  Iniciar(CD, 1, 1)
fin
