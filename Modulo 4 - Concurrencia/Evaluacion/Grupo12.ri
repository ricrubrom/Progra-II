programa Grupo12

procesos


  {Procesos del Cosechador}

  {Gonzalo Blasco - 14/11}
  proceso JuntarFlores(ES flores: numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1
  fin

  {Mateo Romero - 14/11}
  proceso LimpiarEsquina(ES flores: numero; E miAv: numero; E miCa: numero)
  variables 
    randomAv, randomCa : numero
  comenzar
    Random(randomAv, 10, 75)
    Random(randomCa, 10, 40)
    BloquearEsquina(randomAv, randomCa)
    Pos(randomAv, randomCa)
    JuntarFlores(flores)
    Pos(miAv, miCa)
    LiberarEsquina(randomAv, randomCa)
  fin

  {Gonzalo Blasco - 14/11}
  proceso DepositarFlores(E miAv: numero; E miCa: numero)
  comenzar
    BloquearEsquina(100, 1)
    Pos(100, 1)
    mientras(HayFlorEnLaBolsa)
      depositarFlor
    Pos(miAv, miCa)
    LiberarEsquina(100, 1)
  fin
  
  
  {Procesos del Sembrador}

  {Gonzalo Blasco - 14/11}
  proceso RecogerElementos(ES papeles: numero)
  comenzar 
    mientras(HayFlorEnLaEsquina)
      tomarFlor 
    mientras(HayPapelEnLaEsquina)
      tomarPapel
      papeles := papeles + 1
  fin
  
  {Gonzalo Blasco - 14/11}
  proceso Sembrado(E miAv: numero; E miCa: numero; ES papeles: numero; ES sigo: boolean)
  variables
    randomCa : numero
  comenzar
    Random(randomCa, 1, 100)
    BloquearEsquina(100, 1)
    Pos(100, 1)
    si(HayFlorEnLaEsquina)
      tomarFlor
      BloquearEsquina(1, randomCa)
      Pos(1, randomCa)
      LiberarEsquina(100, 1)
      RecogerElementos(papeles)
      depositarFlor
      Pos(miAv, miCa)
      LiberarEsquina(1, randomCa)
    sino
      sigo := F
      Pos(miAv, miCa)
      LiberarEsquina(100,1)
  fin

{Mateo Romero - 14/11}
areas
  AreaRJ : AreaP(50, 50, 50, 50)
  AreaC1 : AreaP(6, 8, 6, 8)
  AreaC2 : AreaP(8, 8, 8, 8)
  AreaC3 : AreaP(10, 8, 10, 8)
  AreaS1 : AreaP(99, 99, 99, 99)
  AreaS2 : AreaP(99, 100, 99, 100)
  
  AreaDeposito : AreaPC(100, 1, 100, 1)

  AreaCosecha : AreaC(10, 10, 75, 40)
  AreaSembrado : AreaC(1, 1, 1, 100)


robots

  {Mateo Romero - 14/11}
  robot Coshechador
  variables
    randomAv, randomCa, miAv, miCa, flores : numero
  comenzar
    flores := 0
    miAv := PosAv
    miCa := PosCa
    repetir(5)
      LimpiarEsquina(flores, miAv, miCa)
    DepositarFlores(miAv, miCa)
    EnviarMensaje(flores, RJ)
  fin

  {Gonzalo Blasco - 14/11}
  robot Sembrador
  variables
    ID, miAv, miCa, papeles: numero
    sigo: boolean
  comenzar
    sigo := V
    papeles := 0
    miAv := PosAv 
    miCa := PosCa
    RecibirMensaje(ID, RJ) 
    mientras(sigo)
      Sembrado(miAv, miCa, papeles, sigo)
    Informar('CantidadDePapeles', papeles)
    EnviarMensaje(ID, RJ)
    EnviarMensaje(papeles, RJ)
  fin

  {Mateo Romero - 14/11}
  robot Jefe
  variables
    totalFlores, receptor, rob, robMin, min : numero
  comenzar
    totalFlores := 0
    rob := 0
    robMin := 0
    min := 99999
    receptor := 0
    repetir(3)
      RecibirMensaje(receptor, *)
      totalFlores := totalFlores + receptor
    Informar('TotalDeFloresRecolectadas', totalFlores)
    EnviarMensaje(1, S1)
    EnviarMensaje(2, S2)
    repetir(2)
      RecibirMensaje(rob, *)
      si(rob = 1)
        RecibirMensaje(receptor, S1)
      sino
        RecibirMensaje(receptor, S2)
      si(receptor < min)
        min := receptor
        robMin := rob
    Informar('RobotConMenorCantidadDePapeles', robMin)
  fin


{Mateo Romero - 14/11}
variables
  C1, C2, C3 : Coshechador
  S1, S2 : Sembrador
  RJ : Jefe

{Mateo Romero - 14/11}
comenzar
  AsignarArea(C1, AreaC1)
  AsignarArea(C1, AreaCosecha)      {Asignaciones Cosechador 1}
  AsignarArea(C1, AreaDeposito)

  AsignarArea(C2, AreaC2)
  AsignarArea(C2, AreaCosecha)      {Asignaciones Cosechador 2}
  AsignarArea(C2, AreaDeposito)

  AsignarArea(C3, AreaC3)
  AsignarArea(C3, AreaCosecha)      {Asignaciones Cosechador 3}
  AsignarArea(C3, AreaDeposito)

  AsignarArea(S1, AreaS1)
  AsignarArea(S1, AreaSembrado)     {Asignaciones Sembrador 1}
  AsignarArea(S1, AreaDeposito)

  AsignarArea(S2, AreaS2)
  AsignarArea(S2, AreaSembrado)     {Asignaciones Sembrador 2}
  AsignarArea(S2, AreaDeposito)

  AsignarArea(RJ, AreaRJ)           {Asignaciones Jefe}

  Iniciar(C1, 6, 8)
  Iniciar(C2, 8, 8)
  Iniciar(C3, 10, 8)                {Inicio de los robots}
  Iniciar(S1, 99,99)
  Iniciar(S2, 99, 100)
  Iniciar(RJ, 50, 50)
fin
